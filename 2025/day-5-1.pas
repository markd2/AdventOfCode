
[INHERIT ('SYS$LIBRARY:STARLET',
          'SYS$LIBRARY:PASCAL$STR_ROUTINES')]

PROGRAM AOC_DAY_5_1 (INFILE, OUTPUT);

TYPE
    STR255 = VARYING [255] OF CHAR;
    BIGNUM = STR255;

    PAIR = RECORD
        LOW: BIGNUM;
        HIGH: BIGNUM;
    END;

VAR
    INFILE: TEXT;
    LINE: STR255;

    PAIRS: ARRAY [1 .. 220] OF PAIR;
    PAIR_COUNT: INTEGER := 0;
    FRESH_PRINTS: INTEGER := 0;

    RANGE_LOW: BIGNUM;
    RANGE_HIGH: BIGNUM;
    SPLITPOINT: INTEGER;
    I: INTEGER;

LABEL
    NEXT;

FUNCTION NUMERIC_COMPARE(THING1, THING2: STR255) : INTEGER; { usual -1/0/+1 }
VAR
    LEN1, LEN2: INTEGER;
BEGIN
    LEN1 := LENGTH(THING1);
    LEN2 := LENGTH(THING2);

    IF LEN1 < LEN2 THEN NUMERIC_COMPARE := -1
    ELSE IF LEN1 > LEN2 THEN NUMERIC_COMPARE := 1
    ELSE BEGIN
        IF THING1 < THING2 THEN NUMERIC_COMPARE := -1
	ELSE IF THING1 > THING2 THEN NUMERIC_COMPARE := 1
        ELSE NUMERIC_COMPARE := 0;
    END;
END;


FUNCTION IS_FRESH(LINE: BIGNUM) : BOOLEAN;
VAR
    I: INTEGER;
BEGIN
    FOR I := 1 TO PAIR_COUNT DO BEGIN
        WITH PAIRS[I] DO BEGIN

            IF (NUMERIC_COMPARE(LINE, LOW) >= 0)  { LINE >= LOW }
               AND (NUMERIC_COMPARE(LINE, HIGH) <= 0) THEN BEGIN { <= HIGH }
               IS_FRESH := TRUE;
               RETURN;
            END;
        END;
    END;
    IS_FRESH := FALSE;
END;

(* MAIN *)
BEGIN
    RESET(INFILE);
    PAIR_COUNT := 0;
    FRESH_PRINTS := 0;

    REPEAT
        READLN(INFILE, LINE);
        IF LENGTH(LINE) = 0 THEN GOTO NEXT;

        SPLITPOINT := INDEX(LINE, "-");
        RANGE_LOW := LINE[1 .. SPLITPOINT - 1];
        RANGE_HIGH := LINE[ SPLITPOINT + 1 .. LENGTH(LINE)];
 
        PAIR_COUNT := PAIR_COUNT + 1;
        PAIRS[PAIR_COUNT].LOW := RANGE_LOW;
        PAIRS[PAIR_COUNT].HIGH := RANGE_HIGH;

    UNTIL EOF(INFILE);

NEXT:
    WRITELN("GOT PAIRS ", PAIR_COUNT);
    REPEAT
        READLN(INFILE, LINE);
        IF IS_FRESH(LINE) THEN BEGIN
            FRESH_PRINTS := FRESH_PRINTS + 1;
        END;
    UNTIL EOF(INFILE);

    WRITELN(FRESH_PRINTS);
END.
